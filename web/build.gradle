plugins {
    id 'org.springframework.boot' version '2.2.6.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.node-gradle.node' version '2.2.3'
    id 'java'
}

ext {
    test_container_version = '1.15.0'
}

dependencies {
    implementation project(':model')
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        exclude module: 'mockito-core'
    }
    testImplementation 'com.ninja-squad:springmockk:2.0.3'
    testCompile "org.testcontainers:junit-jupiter:$test_container_version"
    testCompile "org.testcontainers:postgresql:$test_container_version"
}

bootJar {
    archiveFileName = 'app.jar'
}

npm_run_build {
    inputs.files fileTree('src/main/resources-preprocessed')
    inputs.file 'package.json'
    outputs.dir 'src/main/resources/static'
}

processResources.shouldRunAfter npm_run_build
build.dependsOn npm_run_build
